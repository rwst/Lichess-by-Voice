buildscript {
    ext.kotlin_version = '1.6.20'
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
    }
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' // version '1.6.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.20'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "de.lichessbyvoice"
        minSdk 26
        targetSdk 32
        versionCode 2204
        versionName "22-04"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                appAuthRedirectScheme: 'de.lichessbyvoice'
        ]
    }
    namespace 'de.lichessbyvoice'
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        buildConfig = false
    }
    sourceSets {
        main {
            assets.srcDirs += "$buildDir/generated/assets"
        }
    }
}


tasks.register('genUUID') {
    def uuid = UUID.randomUUID().toString()
    def odir = file("$buildDir/generated/assets/model")
    def ofile = file("$odir/uuid")
    doLast {
        mkdir odir
        ofile.text = uuid
    }
}

preBuild.dependsOn(genUUID)

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.activity:activity-ktx:1.5.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0"
    implementation "org.slf4j:slf4j-simple:2.0.0-alpha7"
    implementation 'net.openid:appauth:0.11.1'
    implementation "com.squareup.okio:okio:3.1.0"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.7"
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.7'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"
    implementation 'net.java.dev.jna:jna:5.11.0@aar'
    implementation group: 'com.alphacephei', name: 'vosk-android', version: '0.3.32'
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}